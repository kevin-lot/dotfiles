LOADED=()
OS=$(uname -s | tr '[:upper:]' '[:lower:]')

autoload -Uz promptinit
promptinit
prompt adam2

bindkey -e

###########################
## history configuration ##
###########################

HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.config/zsh/histfile
setopt histignorealldups
setopt sharehistory # share across all zsh sessions

##############################
## completion configuration ##
##############################

autoload -Uz compinit
compinit
autoload bashcompinit
bashcompinit

setopt menu_complete
setopt auto_cd

zstyle ':completion:*' completer _list _expand _complete _correct _approximate

# disable "show all X possibilities?" message and show menu directly
# disable "Hit TAB for more, or the character to insert" message and show menu directly
zstyle ':completion:*' list-prompt ''
zstyle ':completion:*' select-prompt ''
# and show menu
zstyle ':completion:*' menu yes
zstyle ':completion:*' menu select=2
zstyle ':completion:*' auto-select true
if [[ "${OS}" == "linux" ]]; then
  # with colors
  # http://linuxshellaccount.blogspot.com/2008/12/color-completion-using-zsh-modules-on.html
  # echo ${(s.:.)PATH}: Splits the $PATH variable into a Zsh array based on colon delimiters.
  eval "$(dircolors -b)"
  zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
fi

zstyle ':completion:*' group-name ''
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'

zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
zstyle ':zle:*' word-style subword


#########################
## paths configuration ##
#########################

if [[ "${OS}" == "darwin" ]]; then
    # brew
    [[ -f "/opt/homebrew/bin/brew" ]] && eval "$(/opt/homebrew/bin/brew shellenv)" && rehash && LOADED+=("brew")
fi

export HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE=1
SOURCES=(
    "${HOME}/.config/zsh/zshrc.paths" \
    "${HOME}/.config/zsh/zshrc.alias" \
    "${HOME}/.config/zsh/zshrc.hook" \
    "${HOME}/.zsh_plugins/zsh-autosuggestions/zsh-autosuggestions.zsh" \
    "${HOME}/.zsh_plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" \
    "${HOME}/.zsh_plugins/zsh-history-substring-search/zsh-history-substring-search.zsh"
)
# shellcheck disable=SC1090
for SOURCE in "${SOURCES[@]}"; do [[ -f "${SOURCE}" ]] && source "${SOURCE}" && LOADED+=("${SOURCE}"); done

if [[ "${OS}" == "darwin" ]]; then
    # zsh-history-substring-search
    bindkey '^[[A' history-substring-search-up
    bindkey '^[[B' history-substring-search-down
fi
if [[ "${OS}" == "linux" ]]; then
    # zsh-history-substring-search
    bindkey '^[[A' history-substring-search-up
    bindkey '^[[B' history-substring-search-down
    # shellcheck disable=SC2154
    [[ -n "${terminfo[kcuu1]}" ]] && bindkey "${terminfo[kcuu1]}" history-substring-search-up
    [[ -n "${terminfo[kcud1]}" ]] && bindkey "${terminfo[kcud1]}" history-substring-search-down

    # ctrl to move cursor
    bindkey "^[[1;5C" forward-word
    bindkey "^[[1;5D" backward-word
fi

############################
## activation of programs ##
############################

# starship
type -p starship &>/dev/null && {
	prompt off
	export STARSHIP_CONFIG="${HOME}/.config/starship.toml"
    eval "$(starship init zsh)"
    LOADED+=("starship")
}

# mise
type -p mise &>/dev/null && {
    eval "$(mise activate zsh)"
    eval "$(mise hook-env -s zsh)"
    rehash
    LOADED+=("mise")
}

######################################
## program completion configuration ##
######################################

ZSH_FPATH="${HOME}/.zsh_fpath"
[[ -d "${ZSH_FPATH}" ]] || install -d -m 700 "${ZSH_FPATH}"
[[ -d "${ZSH_FPATH}" ]] && fpath+="${ZSH_FPATH}"
type -p poetry &>/dev/null && poetry completions zsh > "${ZSH_FPATH}/_poetry" || echo "poetry is missing"

type -p aws_completer &>/dev/null && complete -C aws_completer aws || echo "aws_completer is missing"
type -p dagger &>/dev/null && eval "$(dagger completion zsh)" || echo "dagger is missing"
type -p terraform &>/dev/null && complete -o nospace -C terraform terraform || echo "terraform is missing"
type -p terraform-docs &>/dev/null && eval "$(terraform-docs completion zsh)" || echo "terraform-docs is missing"
type -p terragrunt &>/dev/null && complete -o nospace -C terragrunt terragrunt || echo "terragrunt is missing"

##################
## returns info ##
##################

YELLOW='\033[0;34m'
NC='\033[0m'
echo -e "${YELLOW}loads:${NC}"
# shellcheck disable=SC2086 disable=SC2128
print -C3 ${LOADED}
echo
echo -e "${YELLOW}paths:${NC}"
# shellcheck disable=SC2296 disable=SC2086
print -C3 ${(s/:/)PATH}
echo
echo -e "${YELLOW}fpaths:${NC}"
# shellcheck disable=SC2086
print -C3 ${fpath}
